/////////////////////////////////////////////////////////////////
// Sets up the global needed later to put the dagger into play //
// Parsed via framed_quest.tpa                                 //
/////////////////////////////////////////////////////////////////

IF
	Global("#L_FramedQuest","GLOBAL",0)
	Global("#L_FramedQuestSet","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_FramedQuest","GLOBAL",1)
		SetGlobal("#L_FramedQuestSet","MYAREA",1)
		Continue()
END

// Lava allows for his dagger to be tossed
// Which hoses our quest before it has a chance to start
// C'est la vie
IF
	GlobalGT("#L_FramedQuest","GLOBAL",0)
	GlobalGT("L#2SkieDaggerImoen","GLOBAL",49)
THEN
	RESPONSE #100
		SetGlobal("#L_FramedQuest","GLOBAL",0)
		EraseJournalEntry(@3100)
		Continue()
END

// Until the quest is solved, don't allow jumping into Baldur's Gate
IF
	Global("#L_FramedQuest","GLOBAL",2)
	Global("#L_MapSet","MYAREA",0)
	Global("#L_EET","GLOBAL",1)
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobal("#L_MapSet","MYAREA",1)
		RemoveWorldmapAreaFlag("BG0100",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG0200",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG0300",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG0600",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG0700",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG0800",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG1100",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG1200",EXPLORABLE)
		RemoveWorldmapAreaFlag("BG1300",EXPLORABLE)
		EndCutSceneMode()
		Continue()
END

// Quest was cancelled
IF
	Global("#L_FramedQuest","GLOBAL",0)
	Global("#L_MapSet","MYAREA",1)
	Global("#L_EET","GLOBAL",1)
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobal("#L_MapSet","MYAREA",0)
		AddWorldmapAreaFlag("BG0100",EXPLORABLE)
		AddWorldmapAreaFlag("BG0200",EXPLORABLE)
		AddWorldmapAreaFlag("BG0300",EXPLORABLE)
		AddWorldmapAreaFlag("BG0600",EXPLORABLE)
		AddWorldmapAreaFlag("BG0700",EXPLORABLE)
		AddWorldmapAreaFlag("BG0800",EXPLORABLE)
		AddWorldmapAreaFlag("BG1100",EXPLORABLE)
		AddWorldmapAreaFlag("BG1200",EXPLORABLE)
		AddWorldmapAreaFlag("BG1300",EXPLORABLE)
		EndCutSceneMode()
		Continue()
END
