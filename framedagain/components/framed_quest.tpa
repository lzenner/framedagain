///////////////////////////////////////////////////////
// Quest to return soul dagger to Entar Silvershield //
///////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION framed_quest BEGIN

	// Put the required scroll(s) into the game
	ACTION_IF GAME_IS ~eet~ BEGIN
		// Will need to put in all variations of the posters/warrants
		// Which one(s) will actually be found in the dungeon will be dependent upon 
		// the value of bd_player_exiled at the time the PC is transferred to BG2
		OUTER_SET SCROLL_READ1 = RESOLVE_STR_REF (@1001)
		OUTER_SET SCROLL_READ2 = RESOLVE_STR_REF (@1003)
		OUTER_SET SCROLL_READ3 = RESOLVE_STR_REF (@1004)
		COPY ~%mod_root%/objects/#LScr01.itm~ ~override~
    		SAY NAME2 @1000	/* ~Warrant for <CHARNAME>'s Arrest~ */
			SAY DESC  @1001 /* Wanted alive by the dukes for questioning */
		COPY ~%mod_root%/objects/#LScr01.itm~ ~override/#LScr02.itm~
    		SAY NAME2 @1002	/* ~<CHARNAME>'s Wanted Poster~ */
    		SAY DESC  @1003 /* Wanted dead or alive by all the dukes */
		COPY ~%mod_root%/objects/#LScr01.itm~ ~override/#LScr03.itm~
    		SAY NAME2 @1002	/* ~<CHARNAME>'s Wanted Poster~ */
    		SAY DESC  @1004 /* Wanted dead or alive by Entar */

	END ELSE BEGIN	// Stand-alone BG2EE

		OUTER_SPRINT ~GLOBAL_IS_EET~ ~~
		OUTER_SET SCROLL_READ1 = 0
		OUTER_SET SCROLL_READ2 = 0
		OUTER_SET SCROLL_READ3 = RESOLVE_STR_REF (@1004)
		// We have no idea if they even played SoD
		// Just put in the wanted poster from Entar
		COPY ~%mod_root%/objects/#LScr01.itm~ ~override/#LScr03.itm~
    		SAY NAME2 @1002	/* ~<CHARNAME>'s Wanted Poster~ */
    		SAY DESC  @1004 /* Wanted dead or alive by Entar */
    END

	INCLUDE ~framedagain/components/framed_quest_CI.tpa~
	LAF framed_quest_CI END

	// Add the soultaker dagger to the game
	COPY ~%mod_root%/objects/#LDGSoul.BAM~ ~override~
	COPY ~%mod_root%/objects/#LDGSoul.ITM~ ~override~
		SAY NAME2 @1005 /* ~Skie's Soultaker Dagger~ */
		SAY DESC  @1006

	// Lower the drawbridge to Baldur's Gate
	ACTION_IF GAME_IS ~eet~ BEGIN
		LAF INSERT_SCRIPT_BLOCK
			INT_VAR 
				insert_above = 0
				only_once = 1
			STR_VAR 
				script = BG0900
				match = ~SetGlobal("K#Drawbridge","BG0900",1)~
				insert = ~%mod_root%/scripts/quest_bg0900.baf~
		END
	END

	// Define the journal entries to be used during the quest
	ADD_JOURNAL @3000 @3001 USING ~%tra_loc%/%s/framed_quest.tra~
END
